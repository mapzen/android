apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

def API_KEY = hasProperty('apiKey') ? '"' + apiKey + '"' : "null";

android {
  compileSdkVersion 24
  buildToolsVersion "24.0.3"

  defaultConfig {
    applicationId "com.mapzen.android.sdk.sample"
    minSdkVersion 15
    targetSdkVersion 24
    versionCode 1
    versionName "1.0"
    buildConfigField "String", "API_KEY", API_KEY
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    abortOnError false
  }
}

task checkstyle(type: Checkstyle) {
  configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
  source 'src'
  include '**/*.java'
  exclude '**/gen/**'

  classpath = files()
}

task verify(dependsOn: ['compileDebugSources',
                        'test',
                        'checkstyle',
                        'lint'])

dependencies {
  compile(project(':mapzen-android-sdk')) {
    transitive = true;
  }
  compile 'com.android.support:appcompat-v7:24.2.1'
  compile 'com.android.support:design:24.2.1'
}
