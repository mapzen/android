buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
    classpath 'net.researchgate:gradle-release:2.4.0'
    classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.1'
  }
}

apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'net.researchgate.release'

group = GROUP
version = VERSION_NAME
project.archivesBaseName = POM_ARTIFACT_ID

release {
  tagTemplate = 'mapzen-places-api-${version}'
  versionPropertyFile = 'gradle.properties'
  versionProperties = ['VERSION_NAME']
  newVersionCommitMessage = '[Gradle Release Plugin] - mapzen-places-api new version commit:'
}

afterReleaseBuild.dependsOn uploadArchives

android {
  compileSdkVersion 24
  buildToolsVersion "24.0.3"

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 24
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  testOptions {
    unitTests.returnDefaultValues = true
  }
}

tasks.withType(Test) {
  testLogging {
    exceptionFormat "full"
    events "started", "skipped", "passed", "failed"
    showStandardStreams true
  }
}

task checkstyle(type: Checkstyle) {
  configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
  source 'src'
  include '**/*.java'
  exclude '**/gen/**'

  classpath = files()
}

task verify(dependsOn: ['compileDebugSources',
                        'test',
                        'checkstyle',
                        'lint'])

dependencies {
  compile 'com.android.support:appcompat-v7:24.2.1'
  compile 'com.mapzen:mapzen-core:0.0.1-SNAPSHOT'

  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:1.7.1'
  testCompile 'org.powermock:powermock:1.6.4'
  testCompile 'org.powermock:powermock-module-junit4:1.6.4'
  testCompile 'org.powermock:powermock-api-mockito:1.6.4'
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
