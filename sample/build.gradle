apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

def VECTOR_TILES_KEY = hasProperty('vectorTilesKey') ? '"' + vectorTilesKey + '"' : "null";
def SEARCH_KEY = hasProperty('searchKey') ? '"' + searchKey + '"' : "null";
def TURN_BY_TURN_KEY = hasProperty('turnByTurnKey') ? '"' + turnByTurnKey + '"' : "null";

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.3"

  defaultConfig {
    applicationId "com.mapzen.android.sample"
    minSdkVersion 15
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
    buildConfigField "String", "VECTOR_TILES_KEY", VECTOR_TILES_KEY
    buildConfigField "String", "SEARCH_KEY", SEARCH_KEY
    buildConfigField "String", "TURN_BY_TURN_KEY", TURN_BY_TURN_KEY
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    abortOnError false
  }
}

task checkstyle(type: Checkstyle) {
  configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
  source 'src'
  include '**/*.java'
  exclude '**/gen/**'

  classpath = files()
}

task verify(dependsOn: ['test',
                        'checkstyle',
                        'lint'])

dependencies {
  compile(project(':library')) {
    transitive = true;
  }
  compile 'com.android.support:appcompat-v7:23.2.1'
  compile 'com.android.support:design:23.2.1'
}
