buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    classpath 'net.researchgate:gradle-release:2.4.0'
  }
}

apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'net.researchgate.release'

group = GROUP
version = VERSION_NAME
project.archivesBaseName = POM_ARTIFACT_ID

ext.tangram_version = "0.4.9"

release {
  tagTemplate = 'mapzen-core${version}'
  versionPropertyFile = 'gradle.properties'
  versionProperties = ['VERSION_NAME']
  newVersionCommitMessage = '[Gradle Release Plugin] - core new version commit:'
}

android {
  compileSdkVersion 25
  buildToolsVersion '25.0.2'

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 25
    versionCode 1
    versionName "1.0"
    resValue "string", "tangram_version", "${tangram_version}"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  lintOptions {
    abortOnError false
  }
}

task checkstyle(type: Checkstyle) {
  configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
  source 'src'
  include '**/*.java'
  exclude '**/gen/**'

  classpath = files()
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')

task submodules {
  def folder = new File( 'core/src/main/assets/styles/walkabout-style-more-labels/walkabout-style-more-labels.yaml' )
  if(!folder.exists()) {
    throw new GradleException("Submodules aren't present, please run:\n`git submodule init`, " +
        "\n`git submodule update`\nfrom your root directory")

  }
}

assemble.doFirst {
  submodules
}

dependencies {
  compile 'com.android.support:appcompat-v7:25.1.0'

  compile "com.mapzen.tangram:tangram:$tangram_version"
  compile 'com.mapzen.android:pelias-android-sdk:1.1.0-SNAPSHOT'
  compile 'com.mapzen.android:lost:2.1.3-SNAPSHOT'

  compile 'com.google.dagger:dagger:2.0'
  compile 'javax.annotation:javax.annotation-api:1.2'

  annotationProcessor 'com.google.dagger:dagger-compiler:2.0'

  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:1.7.1'
  testCompile 'org.powermock:powermock:1.6.4'
  testCompile 'org.powermock:powermock-module-junit4:1.6.4'
  testCompile 'org.powermock:powermock-api-mockito:1.6.4'
}
